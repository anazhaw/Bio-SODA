# genes in primates orthologous to a gene that is expressed in the fruit fly brain

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX genex: <http://purl.org/genex#>
PREFIX lscr: <http://purl.org/lscr#>
PREFIX orth: <http://purl.org/net/orth#>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT ?gene ?OMA_LINK ?uniprot ?annotation_text WHERE {
	SELECT * {
		SERVICE <https://bgee.org/sparql/> {
			SELECT ?gene {
				?gene genex:isExpressedIn ?anat .
				?anat rdfs:label 'brain' .
				?gene orth:organism ?o .
				?o obo:RO_0002162 ?taxon .
				?taxon up:commonName 'fruit fly' .
			}
			LIMIT 10
		}
		SERVICE <https://sparql.omabrowser.org/sparql/> {
			SELECT ?gene ?OMA_LINK ?uniprot{
				?protein2 a orth:Protein .
				?protein1 a orth:Protein .
				?clusterPrimates a orth:OrthologsCluster .
				?cluster a orth:OrthologsCluster .
				?cluster orth:hasHomologousMember ?node1 .
				?cluster orth:hasHomologousMember ?node2 .
				?node2 orth:hasHomologousMember* ?clusterPrimates .
				?clusterPrimates orth:hasHomologousMember* ?protein2 .
				?node1 orth:hasHomologousMember* ?protein1 .
				?protein1 sio:SIO_010079 ?gene. #encoded by
				?protein2 rdfs:seeAlso ?OMA_LINK .
				?clusterPrimates orth:hasTaxonomicRange ?taxRange .
				?taxRange orth:taxRange ?taxLevel .
				VALUES ?taxLevel { 'Primates' }
				FILTER ( ?node1 != ?node2 )
			}
		}
		SERVICE <https://sparql.uniprot.org/sparql> {
			SELECT ?annotation_text ?uniprot {
				?uniprot up:annotation ?annotation .
				?annotation rdfs:comment ?annotation_text .
			}
		}
	}
}