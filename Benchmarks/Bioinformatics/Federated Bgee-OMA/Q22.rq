# The orthologs of a gene that is expressed in the fruit fly brain

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX genex: <http://purl.org/genex#>
PREFIX lscr: <http://purl.org/lscr#>
PREFIX orth: <http://purl.org/net/orth#>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX sio: <http://semanticscience.org/resource/>
SELECT distinct ?gene_id ?orthologousTo ?uniprot ?annotation_text WHERE {
	SERVICE <https://bgee.org/sparql/> {
		SELECT ?gene ?gene_id {
			?gene a orth:Gene .
			?gene genex:isExpressedIn ?cond .
			?cond genex:hasAnatomicalEntity ?anat .
			?gene dcterms:identifier ?gene_id .
			?anat rdfs:label 'brain' .
			?gene orth:organism ?o .
			?o obo:RO_0002162 ?taxon .
			?taxon up:commonName "fruit fly" .
		}
		LIMIT 10
	}

	SERVICE <https://sparql.omabrowser.org/sparql/> {
		SELECT ?uniprot ?gene ?orthologousTo {
			?cluster a orth:OrthologsCluster .
			?cluster orth:hasHomologousMember ?node1 .
			?cluster orth:hasHomologousMember ?node2 .
			?node2 orth:hasHomologousMember* ?orthologousTo .
			?node1 orth:hasHomologousMember* ?protein1 .
			?protein1 sio:SIO_010079 ?gene .
			filter(?node1 != ?node2)
		}
	}

	SERVICE <http://sparql.uniprot.org/sparql>{
		?uniprot up:annotation ?annotation .
		?annotation rdfs:comment ?annotation_text .
	}
}