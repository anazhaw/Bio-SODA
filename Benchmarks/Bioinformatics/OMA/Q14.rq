# Retrieve all proteins that are orthologous to ENSLACG00000002497 

prefix sio:<http://semanticscience.org/resource/>
select  ?OMA_LINK 
where {
#The three that contains Orthologs. The leafs are proteins.
#This graph pattern defines the relationship protein1 is Orthologs to protein2
?cluster a orth:OrthologsCluster.
?cluster orth:hasHomologousMember ?node1.
?cluster orth:hasHomologousMember ?node2. 
?node2 orth:hasHomologousMember* ?protein2. 
?node1 orth:hasHomologousMember* ?protein1.
########
 
#Specify the protein to look for its orthologs
?protein1 sio:SIO_010079 ?gene . 
?gene lscr:xrefEnsemblGene  ensembl:ENSLACG00000002497.
########
 
#The OMA link to the second protein
?protein2 rdfs:seeAlso ?OMA_LINK. 
 
########
 
filter(?node1 != ?node2) 
}