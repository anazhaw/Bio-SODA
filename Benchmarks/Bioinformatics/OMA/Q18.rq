# Retrieve all proteins per species that are orthologous to Oryctolagus cuniculus' APOCI or APOC1 related protein and their cross reference to Uniprot including Ensembl gene identifier.

prefix sio:<http://semanticscience.org/resource/>
prefix obo:<http://purl.obolibrary.org/obo/>
 
select ?protein1    ?protein2  ?geneName2  ?species2 ?OMA_LINK2 ?Prot2_uniprot ?prot2_ensemblGeneId  
where {
?cluster a orth:OrthologsCluster.
?cluster orth:hasHomologousMember ?node1.
?cluster orth:hasHomologousMember ?node2. 
?node2 orth:hasHomologousMember* ?protein2. 
?node1 orth:hasHomologousMember* ?protein1.
?protein1 a orth:Protein;
orth:organism/obo:RO_0002162/up:scientificName 'Oryctolagus cuniculus';
rdfs:label 'APOCI'.
?protein2 a orth:Protein; 
rdfs:seeAlso ?OMA_LINK2;
lscr:xrefUniprot ?Prot2_uniprot;
sio:SIO_010079/lscr:xrefEnsemblGene/dct:identifier  ?prot2_ensemblGeneId;
rdfs:label ?geneName2;
orth:organism/obo:RO_0002162/up:scientificName ?species2.
filter(?node1 != ?node2)}