# Retrieve Oryctolagus cuniculus' proteins encoded by genes that are orthologous to Mus musculus' HBB-Y gene and their cross reference links to Uniprot

prefix sio:<http://semanticscience.org/resource/>
prefix obo:<http://purl.obolibrary.org/obo/>
 
select distinct ?MOUSE_PROTEIN ?RABIT_PROTEIN ?MOUSE_UNIPROT_XREF ?RABIT_UNIPROT_XREF 
where {
?cluster a orth:OrthologsCluster.
?cluster orth:hasHomologousMember ?node1.
?cluster orth:hasHomologousMember ?node2. 
?node2 orth:hasHomologousMember* ?RABIT_PROTEIN. 
?node1 orth:hasHomologousMember* ?MOUSE_PROTEIN.
?MOUSE_PROTEIN a orth:Protein.
?MOUSE_PROTEIN  orth:organism/obo:RO_0002162/up:scientificName 'Mus musculus';
rdfs:label 'HBB-Y';
lscr:xrefUniprot ?MOUSE_UNIPROT_XREF.
?RABIT_PROTEIN a orth:Protein.
?RABIT_PROTEIN orth:organism/obo:RO_0002162/up:scientificName 'Oryctolagus cuniculus' .
?RABIT_PROTEIN lscr:xrefUniprot ?RABIT_UNIPROT_XREF.
filter(?node1 != ?node2)}